buildscript {
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.2.5.RELEASE"
        classpath "org.akhikhl.gretty:gretty:1.2.4"
        classpath "org.flywaydb:flyway-gradle-plugin:3.2.1"
    }
}

apply plugin: 'groovy'
apply plugin: 'war'
apply plugin: 'idea'
apply plugin: 'jacoco'
apply plugin: 'spring-boot'
apply plugin: "org.akhikhl.gretty"
apply plugin: 'org.flywaydb.flyway'

jar {
    version =  '1.0'
}

ext {
    springBootVersion = "1.2.5.RELEASE"
}


def defaultEncoding = "UTF-8"

tasks.withType(AbstractCompile) { options.encoding = defaultEncoding }
sourceCompatibility = targetCompatibility = JavaVersion.VERSION_1_8

sourceSets {
    integrationTest {
        groovy.srcDir file("src/integrationTest/groovy")
        resources.srcDir file("src/integrationTest/resources")
        compileClasspath = sourceSets.main.output + configurations.testCompile
        runtimeClasspath = output + compileClasspath + configurations.testRuntime
    }
    functionalTest {
        groovy.srcDir file("src/functionalTest/groovy")
        resources.srcDir file("src/functionalTest/resources")
        compileClasspath = sourceSets.main.output + configurations.testCompile
        runtimeClasspath = output + compileClasspath + configurations.testRuntime
    }
}

repositories {
    jcenter()
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.4.3'

    compile "org.springframework.boot:spring-boot-starter-web:$springBootVersion", {
        exclude module: "spring-boot-starter-tomcat"
    }
    compile "org.springframework.boot:spring-boot-starter-jetty:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-actuator:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-thymeleaf:$springBootVersion"
    compile "org.springframework.boot:spring-boot-starter-jdbc:$springBootVersion"

    runtime "com.h2database:h2:1.4.+"
    compile 'org.bgee.log4jdbc-log4j2:log4jdbc-log4j2-jdbc4.1:1.16'

    compile 'org.flywaydb:flyway-core:3.2.1'

    compile 'org.slf4j:slf4j-api:1.7.12'

    // hibernate validator
    runtime 'org.glassfish.web:javax.el:2.2.4'

    testCompile "org.springframework.boot:spring-boot-starter-test:$springBootVersion"

    // integration test
    testCompile 'org.spockframework:spock-spring:1.0-groovy-2.4', {
        exclude group: "org.codehaus.groovy"
    }

    // functional test
    testCompile 'org.gebish:geb-spock:0.10.0', {
        exclude group: "org.codehaus.groovy"
    }
    testCompile 'org.seleniumhq.selenium:selenium-firefox-driver:2.46.0'

}

wrapper {
    gradleVersion = '2.4'
}

gretty {
    springBoot = true
    integrationTestTask = "functionalTest"
}

test {
    reports.html.destination = file("${reports.html.destination}/unit")
    reports.junitXml.destination = file("${reports.junitXml.destination}/unit")
}

task integrationTest(type: Test) {
    group = "verification"

    testClassesDir = sourceSets.integrationTest.output.classesDir
    classpath = sourceSets.integrationTest.runtimeClasspath

    reports.html.destination = file("${reports.html.destination}/integration")
    reports.junitXml.destination = file("${reports.junitXml.destination}/integration")
}

task functionalTest(type: Test) {
    group = "verification"

    testClassesDir = sourceSets.functionalTest.output.classesDir
    classpath = sourceSets.functionalTest.runtimeClasspath

    reports.html.destination = file("${reports.html.destination}/functional")
    reports.junitXml.destination = file("${reports.junitXml.destination}/functional")

    systemProperty 'geb.env', 'firefox'
    systemProperty 'geb.build.reportsDir', reporting.file("geb")

}

check.dependsOn integrationTest
